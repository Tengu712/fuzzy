# string

## General

文字列を表す型。
以下のエスケープ文字をサポートしている。

| 表記 | 意味 |
| --- | --- |
| `\\` | \\ |
| `\"` | " |
| `\r` | CR |
| `\n` | LF |
| `\t` | タブ文字 |
| `\0` | ヌル文字 |

## Functions

### `!`: `@[]`

改行なしで標準出力する。
返戻値は主語。

```fuzzy
"Hello, world!" ! -- Hello, world!と出力される
```

### `!!`: `@[]`

改行ありで標準出力する。
返戻値は主語。

```fuzzy
"Hello, world!" !! -- Hello, world!
                   -- と出力される
```

### `->`: `@['symbol]`

可変変数を定義する。
返戻値は`()`。

```fuzzy
"hello" -> 'greeting. -- greetingという可変変数が定義される
greeting              -- "hello"
```

### `=>`: `@['symbol]`

不変変数を定義する。
返戻値は`()`。

```fuzzy
"world" => 'name. -- nameという不変変数が定義される
name              -- "world"
```

### `#`: `@[]`

文字列の長さを返す。
返戻値の型は`'u32`である。

```fuzzy
"hello" # -- 5
"" #      -- 0
```

### `^`: `@[]`

先頭の文字を返す。
返戻値の型は`'string`である。
空文字列である場合、`()`が返る。

```fuzzy
"hello" ^ -- "h"
"" ^      -- ()
```

### `$`: `@[]`

末尾の文字を返す。
返戻値の型は`'string`である。
空文字列である場合、`()`が返る。

```fuzzy
"hello" $ -- "o"
"" $      -- ()
```

### `@`: `@['i32]`

指定されたインデックスの文字を返す。
返戻値の型は`'string`である。
文字列外のインデックスを指定した場合、`()`が返る。

```fuzzy
"hello" @ 3  -- "l"
"world" @ -1 -- "d"
"!" @ 100    -- ()
```

### `@<`: `@['i32 'string]`

指定されたインデックスに文字を挿入する。
ただし、目的語の文字列は1文字から成る文字列でなければならない。

```fuzzy
"hello" @< 1 "x" -- "hxello"
```

### `@-`: `@['i32]`

指定されたインデックスの文字を削除する。

```fuzzy
"hello" @- 1 -- "hllo"
```

### `$-`: `@[]`

末尾の文字を削除する。

```fuzzy
"hello" $- -- "hell"
```

### `$>`: `@['string]`

主語の末尾に目的語を連結する。

```fuzzy
"Hello, " $> "world!" -- "Hello, world!"
```

### `=@`: `@['string 'string]`

主語内の目的語1を目的語2に置換する。

```fuzzy
"Hello, world!" =@ "world" "fuzzy" -- "Hello, fuzzy!"
"foo bar baz" =@ "ba" "BA"         -- "foo BAr BAz"
```

### `<`: `@['string]`

主語が目的語より小さいかを判定する。

```fuzzy
"" < ""    -- ()
"a" < "ab" -- T
"2" < "10" -- ()
```

### `<=`: `@['string]`

主語が目的語以下かを判定する。

```fuzzy
"" <= ""    -- T
"a" <= "ab" -- T
"2" <= "10" -- ()
```

### `>`: `@['string]`

主語が目的語より大きいかを判定する。

```fuzzy
"" > ""    -- ()
"a" > "ab" -- ()
"2" > "10" -- T
```

### `>=`: `@['string]`

主語が目的語以上かを判定する。

```fuzzy
"" >= ""    -- T
"a" >= "ab" -- ()
"2" >= "10" -- T
```

### `==`: `@['string]`

主語が目的語と同値かを判定する。

```fuzzy
"" == ""    -- T
"a" == "ab" -- ()
```

### `!=`: `@['string]`

主語が目的語と異値かを判定する。

```fuzzy
"" != ""    -- ()
"a" != "ab" -- T
```
